plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.kloenlansfiel.jiconfont'
version '1.2.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.github.jiconfont:jiconfont:1.0.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            afterEvaluate {
                artifactId = "mana"
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'jIconFont Mana'
                    description = 'jIconFont (https://github.com/jIconFont/jiconfont) implementation of Mana (https://github.com/andrewgioia/Mana)'
                    url = 'https://github.com/Kloen/jiconfont-mana'
                    organization {
                        name = 'com.kloenlansfiel'
                        url = 'https://www.kloenlansfiel.com'
                    }
                    issueManagement {
                        system = 'Github'
                        url = 'https://github.com/Kloen/jiconfont-mana/issues'
                    }
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/Kloen/jiconfont-mana/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'kloen'
                            name = 'Kl√∂en Lansfiel'
                            email = 'kloen@kloenlansfiel.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/Kloen/jiconfont-mana.git'
                        connection = 'scm:git:git://github.com/Kloen/jiconfont-mana.git'
                        developerConnection = 'scm:git:ssh://git@github.com:Kloen/jiconfont-mana.git'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
